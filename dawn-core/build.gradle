buildscript {
    repositories {
        jcenter()
    }
}

allprojects{
    apply plugin: 'maven'
    apply plugin: 'java'
    apply plugin: 'eclipse'
    
    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
    
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    
    configurations.all {
        resolutionStrategy {
            cacheChangingModulesFor 0, 'seconds'
            cacheDynamicVersionsFor 0, 'seconds'
        }
    }
   
    // def localMavenRepo = 'file://' + new File(System.getProperty('user.home'), '.m2/repository').absolutePath
    
    repositories {
        maven { url property('mavenUrl') }
        mavenCentral()
    }
    
    uploadArchives.repositories.mavenDeployer {
        // repository(url: localMavenRepo)
        repository(url: property('releaseUploadUrl')) {
            authentication(userName: property('releaseUploadUserName'), password: property('releaseUploadPassword'))
        }
        snapshotRepository(url: property('snapshotUploadUrl')) {
            authentication(userName: property('snapshotUploadUserName'), password: property('snapshotUploadPassword'))
        }
    }
    
    configurations {
        provided
    }
    
    sourceSets {
        main.compileClasspath += configurations.provided
        test.compileClasspath += configurations.provided
        test.runtimeClasspath += configurations.provided
    }
    eclipse.classpath.plusConfigurations += [ configurations.provided ]
    
    dependencies {
       compile('org.apache.commons:commons-lang3:3.3.2')
       compile('commons-codec:commons-codec:1.4')
       compile('commons-collections:commons-collections:3.2.2')
       compile('commons-beanutils:commons-beanutils:1.9.3')
       compile('org.slf4j:slf4j-api:1.7.7')
       compile('junit:junit:4.4')
       compile('com.alibaba:fastjson:1.2.57')
       
       // 本地测试用（打包去掉）
       compile('ch.qos.logback:logback-classic:1.1.2')
       compile('ch.qos.logback:logback-core:1.1.2')
    }
    
    group = 'com.sf.dawn'
    version =  '2.1.3'
    jar {
        baseName = project.name
        manifest {
            attributes 'Implementation-Title': 'Dawn Core Project', 'Implementation-Version': version
            attributes 'Main-Class': 'com.sf.dawn.core.main.Main'
        }
    }
    
    task initSourceFolders {
        sourceSets*.java.srcDirs*.each { it.mkdirs() }
        sourceSets*.resources.srcDirs*.each { it.mkdirs() }
    }
    
    task wrapper(type: Wrapper) {
        gradleVersion = '4.0.2'
    }

}
